# -----------------------------------------------------------------
# Programmer(s): Slaven Peles @ LLNL
# -----------------------------------------------------------------
# SUNDIALS Copyright Start
# Copyright (c) 2002-2019, Lawrence Livermore National Security
# and Southern Methodist University.
# All rights reserved.
#
# See the top-level LICENSE and NOTICE files for details.
#
# SPDX-License-Identifier: BSD-3-Clause
# SUNDIALS Copyright End
# -----------------------------------------------------------------
# CMakeLists.txt for @SOLVER@ Trilinos examples.
#
# This file is generated from a template using various variables
# set at configuration time. It can be used as a template for
# other user CMakeLists configuration files.
# -----------------------------------------------------------------

cmake_minimum_required(VERSION 3.5)

# Specify project name
project(@SOLVER@_CXX_trilinos_examples)

# Enable testing
include(CTest)

# Include Trilinos configuration file
set(Trilinos_DIR @Trilinos_DIR@)
find_package(Trilinos REQUIRED)

# Use same compilers you used to build SUNDIALS to build examples
# Set Trilinos compilers
set(CMAKE_CXX_COMPILER ${Trilinos_CXX_COMPILER})
set(CMAKE_C_COMPILER   ${Trilinos_C_COMPILER})

# Set compiler flags
set(CMAKE_CXX_FLAGS ${Trilinos_CXX_COMPILER_FLAGS})
set(CMAKE_C_FLAGS   ${Trilinos_C_COMPILER_FLAGS})

mark_as_advanced(EXECUTABLE_OUTPUT_PATH LIBRARY_OUTPUT_PATH)

# Set the names of the examples to be built
set(examples @EXAMPLES@)
list(REMOVE_DUPLICATES examples)

# Set names of examples specific dependency source files
set(examples_dependencies @EXAMPLES_DEPENDENCIES@)

# Specify path to SUNDIALS header files
set(SUNDIALS_INC_DIR
  @includedir@
  CACHE STRING
  "Location of SUNDIALS header files")

# Add path to SUNDIALS header files
include_directories(${SUNDIALS_INC_DIR} ${Trilinos_INCLUDE_DIRS})

# Set search path for SUNDIALS libraries
set(SUNDIALS_LIB_DIR @libdir@)

# Find the SUNDIALS solver's library
find_library(SUNDIALS_SOLVER_LIB
  @SOLVER_LIB@ ${SUNDIALS_LIB_DIR}
  DOC "@SOLVER@ library")

# Find the NVECTOR library
find_library(SUNDIALS_NVEC_LIB
  sundials_nvectrilinos ${SUNDIALS_LIB_DIR}
  DOC "NVECTOR library")

# Set additional libraries
set(SUNDIALS_EXTRA_LIB @LIBS@ CACHE STRING "Additional libraries")

# List of all libraries
set(SUNDIALS_LIBS ${SUNDIALS_SOLVER_LIB} ${SUNDIALS_NVEC_LIB} ${SUNDIALS_EXTRA_LIB})

# Build each example one by one
foreach(example ${examples})

  # example source files
  add_executable(${example} ${example}.cpp ${examples_dependencies})

  # libraries to link against
  target_link_libraries(${example} ${SUNDIALS_LIBS} ${Trilinos_LIBRARIES})

  # add the example to ctest
  add_test(NAME ${example} COMMAND ${example})

endforeach(example ${examples})
