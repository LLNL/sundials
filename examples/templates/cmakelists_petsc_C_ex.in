# -----------------------------------------------------------------
# Programmer: Radu Serban, Cody Balos @ LLNL
# -----------------------------------------------------------------
# SUNDIALS Copyright Start
# Copyright (c) 2002-2019, Lawrence Livermore National Security
# and Southern Methodist University.
# All rights reserved.
#
# See the top-level LICENSE and NOTICE files for details.
#
# SPDX-License-Identifier: BSD-3-Clause
# SUNDIALS Copyright End
# -----------------------------------------------------------------
# CMakeLists.txt for @SOLVER@ petsc examples
#
# This file is generated from a template using  various variables
# set at configuration time. It can be used as a template for
# other user CMakeLists configuration files.
# -----------------------------------------------------------------

cmake_minimum_required(VERSION 3.5)

# Specify project name
project(@SOLVER@_petsc_examples C)

# Enable testing
include(CTest)

mark_as_advanced(EXECUTABLE_OUTPUT_PATH LIBRARY_OUTPUT_PATH)

# Set the names of the examples to be built
set(examples @EXAMPLES@)

# Set names of examples specific dependency source files
set(examples_dependencies @EXAMPLES_DEPENDENCIES@)

# Set name of MPI compiler to be used (CC or MPICC)
set(MPI_MPICC @MPICC@ CACHE STRING "MPICC compiler script")
set(CMAKE_C_COMPILER ${MPI_MPICC})

# Specify path to SUNDIALS header files
set(SUNDIALS_INC_DIR
  @includedir@
  CACHE STRING
  "Location of SUNDIALS header files")

# Add path to SUNDIALS header files
include_directories(${SUNDIALS_INC_DIR})

# Set search path for SUNDIALS libraries
set(SUNDIALS_LIB_DIR @libdir@)

# Find the SUNDIALS solver's library
find_library(SUNDIALS_SOLVER_LIB
  @SOLVER_LIB@ ${SUNDIALS_LIB_DIR}
  DOC "@SOLVER@ library")

# Find the NVECTOR PETSC library
find_library(SUNDIALS_NVEC_LIB
  sundials_nvecpetsc ${SUNDIALS_LIB_DIR}
  DOC "NVECTOR library")

# Find the SUNNonlinearSolver PETSC library
FIND_LIBRARY(SUNDIALS_NLS_LIB
  sundials_sunnonlinsolpetscsnes ${SUNDIALS_LIB_DIR}
  DOC "SUNNonlinearSolver library")


# Set additional libraries
set(SUNDIALS_EXTRA_LIB @LIBS@ CACHE STRING "Additional libraries")

# Set PETSc include and libraries
include_directories(@PETSC_INCLUDE_DIR@)
set(PETSC_LIBRARIES @PETSC_LIBRARIES@ CACHE STRING "PETSC libraries")

# List of all libraries
set(SUNDIALS_LIBS ${SUNDIALS_SOLVER_LIB} ${SUNDIALS_NVEC_LIB} ${SUNDIALS_NLS_LIB} ${SUNDIALS_EXTRA_LIB})

# Build each example one by one
foreach(example ${examples})

  # example source files
  add_executable(${example} ${example}.c ${examples_dependencies})

  # libraries to link against
  target_link_libraries(${example} ${SUNDIALS_LIBS} ${PETSC_LIBRARIES})

  # add the example to ctest
  add_test(NAME ${example} COMMAND ${example})

endforeach(example ${examples})
